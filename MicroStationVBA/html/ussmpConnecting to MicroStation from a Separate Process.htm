<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=Windows-1252">
<TITLE>Connecting to MicroStation from a Separate Process</TITLE>
<style>@import url(/Office.css);</style>
<link disabled rel="stylesheet" href="/MSOffice.css"></HEAD>
<BODY BGCOLOR="#FFFFFF" TEXT="#000000">

<H1 class=RH1>Connecting to MicroStation from a Separate Process</H1>

<PRE class=CT><CODE>
'
'   This example shows different ways of accessing MicroStation from a separate process.
'   All of the code in this example was tested in a VBA project in Microsoft Word.
'
'   The most significant point in this example is the difference between
'   "New ApplicationObjectConnector" and "New MicroStationDGN.Application".
'   "New ApplicationObjectConnector" always starts a new MicroStation process.
'   "New MicroStationDGN.Application" attaches to an existing MicroStation
'   process if there is one but starts a new one otherwise.
'

'
'  Using the Application Object Connector and New
'
Sub StartMicroStation(designFileName As String)
    Dim oAL As <A href="usobjApplicationObjectConnector.htm">ApplicationObjectConnector</A>
    Dim o As MicroStationDGN.<A href="usproApplication.htm">Application</A>

    '  This always starts a new MicroStation process even if there is already
    '  an active MicroStation
    Set oAL = New ApplicationObjectConnector

    Set o = oAL.Application
    o.<A href="usproVisible.htm">Visible</A> = True
    o.<A href="usmthOpenDesignFile.htm">OpenDesignFile</A> designFileName
End Sub
'
'   Using CreateObject on ApplicationObjectConnector. This is essentially
'   the same as New ApplicationObjectConnector.
'
Sub StartMicroStation2(designFileName As String)
    Dim oAL As ApplicationObjectConnector
    Dim o As MicroStationDGN.Application

    '  This always starts a new MicroStation process even if there is already
    '  an active MicroStation
    Set oAL = CreateObject("MicroStationDGN.ApplicationObjectConnector")

    Set o = oAL.Application
    o.Visible = True
    o.OpenDesignFile designFileName
End Sub
'
'   Using the Application Object Connector and GetObject
'
Sub UseActiveConnector()
    Dim oAL As ApplicationObjectConnector
    Dim o As MicroStationDGN.Application

    '  This gets the ApplicationObjectConnector if MicroStation is already running
    '  and raises an error otherwise.  If there are multiple MicroStation processes
    '  running, it attaches to the process that was started first.
    Set oAL = GetObject(, "MicroStationDGN.ApplicationObjectConnector")

    Set o = oAL.Application
    o.<A href="usmthQuit.htm">Quit</A>
End Sub
'
'   Using New MicroStationDGN.Application
'
Sub StartOrAttach(designFileName As String)
    Dim o As MicroStationDGN.Application

    '  This attaches to a running MicroStation if there is one. If
    '  MicroStation is not running, it starts a new MicroStation
    '  process and attaches to it.
    Set o = New MicroStationDGN.Application

    o.Visible = True
    o.OpenDesignFile designFileName
End Sub
'
'   Using CreateObject on "MicroStationDGN.Application".  This
'   is essentially the same as New MicroStationDGN.Application
'
Sub StartOrAttach2(designFileName As String)
    Dim o As MicroStationDGN.Application

    '  This attaches to a running MicroStation if there is one. If
    '  MicroStation is not running, it starts a new MicroStation
    '  process and attaches to it.
    Set o = CreateObject("MicroStationDGN.Application")

    o.Visible = True
    o.OpenDesignFile designFileName
End Sub

'
'   Using GetObject on "MicroStationDGN.Application"
'
Sub UseActiveApplication()
    Dim o As MicroStationDGN.Application

    '  This gets the Application object if MicroStation is already running
    '  and raises an error otherwise.  If there are multiple MicroStation processes
    '  running, it attaches to the process that was started first.
    Set o = GetObject(, "MicroStationDGN.Application")

    o.Quit
End Sub

</CODE></PRE>
